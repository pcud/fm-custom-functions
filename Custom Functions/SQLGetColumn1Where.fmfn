/*
 *  CUSTOM FUNCTION
 *    SQLGetColumn1Where ( returnField ; whereField ; whereValueList; andSearch )
 *
 *  DESCRIPTION
 *    Does a simple SQL Statement with where query(ies). If the whereField is left blank, will return all of the values in returnField
 *
 *  PARAMS
 *    returnField: The field you want to return the value of
 *    whereField: The field that contains the value to filter for
 *    whereValueList: A list of values to filter for
 *    andSearch: Set to true to do an and search with each value in where value. Otherwise will be or
 *
 *  RETURNS
 *    A list of values
 *
 *  DEPENDENCIES
 *    SQLGetField: (https://www.briandunning.com/cf/2546)
 *    SQLGetTable: (https://www.briandunning.com/cf/2547)
 *
 *  CHANGELOG
 *    2019-10-16: Paul McCudden: Created
 *    2021-11-03: Paul McCudden: Added the ability to pass a list into whereValue
*/

While (
  // initialVariable
  [
    ~querry = "SELECT " & SQLGetField ( returnField ) & " FROM " & SQLGetTable ( returnField );
    ~params = "";
    ~i = 0;
    ~count = ValueCount ( whereValueList )
  ];

  // condition
  ~i < ~count;

  // logic
  [
    ~i = ~i + 1;
    ~querry = ~querry &"¶  "& If ( ~i > 1; If ( andSearch; "AND "; "OR " ); "WHERE " ) & SQLGetField ( whereField ) & " = ?";
    ~params = ~params & "; \"" & GetValue ( whereValueList ; ~i ) & "\""
  ];

  // result
  Evaluate (
    "ExecuteSQL (¶" &
    "  \"" & Substitute ( ~querry; "\""; "\\\"" ) & "\"¶" &
    "  ; \"\"; \"\"" & ~params &¶&
    ")" 
  )

)