/*
 *  CUSTOM FUNCTION
 *    ScriptEnvironmentObject ( additionalJSON )
 *
 *  DESCRIPTION
 *    Has the default JSONObject that should be the parameter for the enter script enviornment and exit script envrionemnt steps
 *    Any data passed with additionalJSON
 *
 *  PARAMS
 *    additonalJSON: More json that you wish to include in the object
 *
 *  RETURNS
 *    Object
 *
 *  DEPENDENCIES
 *    JSONDeepMergeObjects: https://github.com/pcud/fm-custom-functions/blob/master/Custom%20Functions/JSONDeepMergeObjects.fmfn
 *    JSONGetElementTypeExtended: https://github.com/pcud/fm-custom-functions/blob/master/Custom%20Functions/JSONGetElementTypeExtended.fmfn
 *
 *  CHANGELOG
 *    2022-12-05: Paul McCudden: Created
 *    2023-05-09: Paul McCudden: Added error data
 *    2023-12-05: Paul McCudden: Prioritized JSON data being passed into additionalJSON
 *    2023-12-07: Paul McCudden: Bug fixes
 *
*/

Let (
    [
        $scriptLogID = If ( IsEmpty ( $scriptLogID ); "ScriptLog-" & Get ( UUID ); $scriptLogID );
        $return = If ( $return = ""; "{}"; $return);
        ~jsonToAdd = If ( Left ( JSONFormatElements ( additionalJSON ); 1 ) = "?"; "{}"; additionalJSON );

        // Assume that if there isn't a return key, everything should be a child of return
        ~jsonToAdd = Case (
            IsEmpty ( FilterValues ( JSONListKeys ( ~jsonToAdd; "" ); "return" ) ) and ~jsonToAdd <> "{}";
                JSONSetElement ( "{}"; "return"; ~jsonToAdd; JSONGetElementTypeExtended ( ~jsonToAdd; "") );
            ~jsonToAdd
        );
        ~jsonExisting = JSONSetElement ( "{}"
            ;["scriptLogID"; $scriptLogID; JSONString]
            ;["return"; $return; JSONGetElementTypeExtended ( $return; "")]
            ;["file"; Get ( FileName ); JSONString]
            ;["script"; Get ( ScriptName ); JSONString]
            ;["errorData"; $errorData; JSONArray]
        )
    ];

    JSONDeepMergeObjects ( ~jsonExisting; ~jsonToAdd )

)