 /*
 *  CUSTOM FUNCTION
 *    ErrorArrayDisplay ( errorArray )
 *
 *  DESCRIPTION
 *	  Returns human readable error information based on the data generated by the ErrorArrayAppend CF
 *
 *  PARAMS
 *    errorArray: The array of error information generated by ErrorArrayAppend
 *
 *  RETURNS
 *    String
 *
 *  DEPENDENCIES
 *    ErrorArrayAppend
 *
 *  CHANGELOG
 *    2023-06-28: Paul McCudden: Created
 *    2024-12-12: Paul McCudden: Updated name to distinguish between other error methods
 *    2025-07-01: Paul McCudden: Updated to pull in $errorData if no passed errorArray
 *
*/

While (
    [
        ~errorArray = JSONSetElement ( "[]"; ""; If ( IsEmpty ( errorArray ); $errorData; errorArray ); JSONArray );
        ~indexCount = ValueCount ( JSONListKeys ( ~errorArray; "" ) );
        ~i = 0;
        ~message = ""
    ];

    ~errorArray <> "[]" and ~i < ~indexCount;

    [
        ~i = ~i + 1;
        ~thisErrorObject = JSONGetElement ( errorArray; "[" & ~i - 1 & "]");
        ~code = JSONGetElement ( ~thisErrorObject; "errorCode" );
        ~description = JSONGetElement ( ~thisErrorObject; "errorDescription" );
        ~info = JSONGetElement ( ~thisErrorObject; "errorInfo" );
        ~message = If ( ~i > 1; ~message & ¶ ) & 
            Trim (
                If ( not IsEmpty ( ~info ) and ~info <> "?"; ~info & If ( Right ( ~info; 1 ) <> "."; ".") & " " ) &
                If ( not IsEmpty ( ~description ) and ~description <> "?"; ~description & " " ) &
                If ( not IsEmpty ( ~code ) and ~code <> "?"; "(Error " & ~code & ")" )
            )
    ];

    Case (
        ~errorArray = "[]"; "";
        ValueCount ( ~message ) > 1; "Error Messages:¶" & "  -" & Substitute ( ~message; ¶; "¶  -" );
        ~message
    )

)
