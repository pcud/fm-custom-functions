/*
 *  CUSTOM FUNCTION
 *    GetScriptResultSuccess ( result )
 *
 *  DESCRIPTION
 *    If used with scripting conventions will let you know if the script ran successfully or not
 *
 *  PARAMS
 *    result: The script result. If blank will use Get ( ScriptResult )
 *
 *  RETURNS
 *    Boolean
 *
 *  DEPENDENCIES
 *    JSONArrayAppend: https://github.com/pcud/fm-custom-functions/blob/master/Custom%20Functions/JSONArrayAppend.fmfn
 *
 *  CHANGELOG
 *    2023-11-02: Paul McCudden: Created
 *    2023-12-28: Updated to append the new $errorData to the current $errorData
 *.   2025-07-01: Paul McCudden: Took out appending $errorData to the current $errorData
*/ 

Let (
    [
        ~result = If ( IsEmpty ( result ); Get ( ScriptResult ); result );
        ~result = If ( Left ( JSONFormatElements ( ~result ); 1 ) = "?"; "{}"; ~result );

        // Pass up the common variables to the parent script so it can be used in logging
        $error = JSONGetElement ( ~result; "error" );

        // Don't pass up error data since the sub script should log it
        // $errorData = JSONArrayAppend ( $errorData; ""; JSONGetElement ( ~result; "errorData" ); JSONObject );
        $message = JSONGetElement ( ~result; "message" )
    ];

    // Check to see if this value or a nested value is successful
    GetAsBoolean ( JSONGetElement ( ~result; "return.success" ) or JSONGetElement ( ~result; "success" ) )

)