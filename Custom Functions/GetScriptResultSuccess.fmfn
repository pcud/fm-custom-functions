/*
 *  CUSTOM FUNCTION
 *    GetScriptResultSuccess ( result )
 *
 *  DESCRIPTION
 *    If used with scripting conventions will let you know if the script ran successfully or not
 *
 *  PARAMS
 *    result: The script result. If blank will use Get ( ScriptResult )
 *
 *  RETURNS
 *    Boolean
 *
 *  DEPENDENCIES
 *    None
 *
 *  CHANGELOG
 *    2023-11-02: Paul McCudden: Created
*/ 

Let (
    [
        ~result = If ( IsEmpty ( result ); Get ( ScriptResult ); result );
        ~result = If ( Left ( JSONFormatElements ( ~result ); 1 ) = "?"; "{}"; ~result );

        // Pass up the common variables to the parent script so it can be used in logging
        $error = JSONGetElement ( ~result; "error" );
        $errorData = JSONGetElement ( ~result; "errorData" );
        $message = JSONGetElement ( ~result; "message" )
    ];

    // Check to see if this value or a nested value is successful
    GetAsBoolean ( JSONGetElement ( ~result; "return.success" ) or JSONGetElement ( ~result; "success" ) )

)