/*
 *  CUSTOM FUNCTION
 *    FormatFriendlyTime ( theTime )
 *
 *  DESCRIPTION
 *    Formats time to display in a user friendly way.
 *
 *  PARAMS
 *    theTime: The time to format
 *
 *  RETURNS
 *    Text
 *
 *  DEPENDENCIES
 *    —None—
 *
 *  CHANGELOG
 *    2023-07-26: Paul McCudden: Created
 *    2024-04-26: Paul McCudden: If time is 0, output 0s
 *    2024-05-30: Paul McCudden: Updated to handle seconds better
 *
*/

Let (
	[
		~hours = Hour ( theTime );
        ~min = Minute ( theTime );
        ~sec = If ( ~hours = 0 and ~min = 0;
			Seconds ( theTime );
			Round ( Seconds ( theTime ); 0 )
		)
	];

	Trim (
		If ( ~hours > 0; ~hours & "h " ) &
		If ( ~min > 0; Right ( "0" & ~min; 2 ) & "m " ) &
        If ( ~hours = 0 and ~min = 0;
			If ( Int ( ~sec ) = 0; "0" ) & ~sec & "s";
			If ( ~sec > 0; Right ( "0" & ~sec; 2 ) & "s" )
		)
	)
)